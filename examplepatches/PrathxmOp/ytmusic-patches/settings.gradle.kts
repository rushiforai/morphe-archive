rootProject.name = "morphe-patches"

pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        google()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MorpheApp/registry")
            credentials {
                username = System.getenv("ORG_GRADLE_PROJECT_gpr_user")
                    ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: System.getenv("GPR_USER")
                        ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: providers.gradleProperty("gpr.user").orNull
                        ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: System.getenv("GITHUB_ACTOR")
                        ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: ""
                password = System.getenv("ORG_GRADLE_PROJECT_gpr_key")
                    ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: System.getenv("GPR_KEY")
                        ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: providers.gradleProperty("gpr.key").orNull
                        ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: System.getenv("GITHUB_TOKEN")
                        ?.takeIf { it.isNotBlank() && it.lowercase() != "dummy" }
                    ?: ""
            }
        }
        // Obtain baksmali/smali from source builds - https://github.com/iBotPeaches/smali
        // Remove when official smali releases come out again.
        maven { url = uri("https://jitpack.io") }
    }
}

plugins {
    id("app.morphe.patches") version "1.3.2"
}

settings {
    extensions {
        defaultNamespace = "app.morphe.extension"

        // Must resolve to an absolute path (not relative),
        // otherwise the extensions in subfolders will fail to find the proguard config.
        proguardFiles(rootProject.projectDir.resolve("extensions/proguard-rules.pro").toString())
    }
}

include(":patches:stub")

// Include morphe-patcher as composite builds if they exist locally
mapOf(
    "morphe-patcher" to "app.morphe:morphe-patcher",
).forEach { (libraryPath, libraryName) ->
    val libDir = file("../$libraryPath")
    if (libDir.exists()) {
        includeBuild(libDir) {
            dependencySubstitution {
                substitute(module(libraryName)).using(project(":"))
            }
        }
    }
}

// Include morphe-patches-library as composite build if it exists locally.
// It is a multi-module project, so each artifact maps to a specific subproject.
file("../morphe-patches-library").let { libDir ->
    if (libDir.exists()) {
        includeBuild(libDir) {
            dependencySubstitution {
                substitute(module("app.morphe:morphe-patches-library")).using(project(":patch-library"))
                substitute(module("app.morphe:morphe-extensions-library")).using(project(":extension-library"))
            }
        }
    }
}
