group = "app.prathxm.chess"

patches {
    about {
        name = "Prathxm Patches"
        description = "Custom Morphe patches for Chess.com — Ad-free experience and local offline engine tools"
        source = "git@github.com:PrathxmOp/Prathxm-Patches.git"
        author = "Prathxm"
        contact = "github.com/PrathxmOp"
        website = "github.com/PrathxmOp/Prathxm-Patches"
        license = "GPLv3"
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xcontext-parameters")
    }
}

// Separate configuration so gson is available at runtime for the
// generatePatchesList task but never bundled into the APK.
val patchListGeneratorClasspath: Configuration by configurations.creating

dependencies {
    compileOnly(libs.gson)
    patchListGeneratorClasspath(libs.gson)
}

tasks {
    register<JavaExec>("generatePatchesList") {
        description = "Build patch with patch list"

        dependsOn(build)

        classpath = sourceSets["main"].runtimeClasspath + patchListGeneratorClasspath
        mainClass.set("util.PatchListGeneratorKt")
    }

    // Used by gradle-semantic-release-plugin.
    publish {
        dependsOn("generatePatchesList")
    }

}